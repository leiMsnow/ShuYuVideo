apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.0.0'
    }
}
greendao {
    schemaVersion 1
    daoPackage 'com.shuyu.video.db.dao'
    targetGenDir 'src/main/java-gen'
}
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId "com.shuyu.video"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        multiDexEnabled true

    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }
    signingConfigs {

        release {
            keyAlias 'shuyouvideo'
            keyPassword 'shuyouvideo123'
            storeFile file('../shuyou.jks')
            storePassword 'shuyou123'
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java-gen']
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }


    buildTypes {
        debug {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "IS_DEBUG", "true"
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "debug影院"
//            minifyEnabled true
//            zipAlignEnabled true
//            // 移除无用的resource文件
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "IS_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName =
                                "shuyuvideo_${variant.productFlavors[0].name}_" +
                                "${defaultConfig.versionName}_${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    // 多渠道打包
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "dev影院"
            buildConfigField "String", "ENVIRONMENT", '"dev"'
        }
        online {
            applicationIdSuffix ".online"
            resValue "string", "app_name", "红人影院"
            buildConfigField "String", "ENVIRONMENT", '"online"'
        }
    }
}

def releaseTime() {
    return new Date().format("yyyyMMddHHmmss")
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':corelibrary')
    compile project(':universalvideoview')
}
